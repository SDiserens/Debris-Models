	SUBROUTINE SFLUXD (RJUD, SD, OUTR)
C
C-----
C
C PURPOSE:
C
C       THE SUBROUTINE SFLUXD RETURNS WITH  THE  SOLAR  FLUX
C                                                -      ----
C       AND GEO-PHYSICAL DATA AT THE DATE RJUD.
C                        -
C INPUTS:
C
C       RJUD  MODIFIED JULIAN DATE  (IF  OUT  OF  RANGE  THE 
C             ROUTINE WILL RETURN A NON-ZERO VALUE IN OUTR.
C			(REAL*8)
C
C OUTPUTS:
C
C       SD(1) MODIFIED JULIAN DATE (1950.0) OF THE INPUT RECORD
C       SD(2) THREE-HOURLY GEOMAGNETIC PLANETARY INDICE  KP,
C             FOR 0-3 UTC.
C       SD(3) THREE-HOURLY GEOMAGNETIC PLANETARY INDICE  KP,
C             FOR 3-6 UTC.
C       SD(4) THREE-HOURLY GEOMAGNETIC PLANETARY INDICE  KP,
C             FOR 6-9 UTC.
C       SD(5) THREE-HOURLY GEOMAGNETIC PLANETARY INDICE  KP,
C             FOR 9-12 UTC.
C       SD(6) THREE-HOURLY GEOMAGNETIC PLANETARY INDICE  KP,
C             FOR 12-15 UTC.
C       SD(7) THREE-HOURLY GEOMAGNETIC PLANETARY INDICE  KP,
C             FOR 15-18 UTC.
C       SD(8) THREE-HOURLY GEOMAGNETIC PLANETARY INDICE  KP,
C             FOR 18-21 UTC.
C       SD(9) THREE-HOURLY GEOMAGNETIC PLANETARY INDICE  KP,
C             FOR 21-24 UTC.
C       SD(10) DAILY SUMM OF  PLANETARY  GEOMAGNETIC  INDICES
C             KP
C       SD(11) DAILY EQUIVALENT PLANETARY AMPLITUDE AP
C       SD(12) DAILY OBSERVED SOLAR FLUX AT  2800  MHZ  (10.7
C             CM) IN 1E-22 W/M/M/HZ.
C       SD(13) ADJUSTED TO 1 AU DAILY SOLAR  FLUX,  IN  1E-22 
C             W/M/M/HZ.
C       SD(14) AVERAGED DAILY OBSERVED SOLAR FLUX, AS  DEFI-
C             NED BY JACCHIA (1).
C       SD(15) ARITMETIC MEAN OF DAILY OBSERVED SOLAR  FLUX,
C             AS DEFINED BY JACCHIA (2).
C
C       OUTR  OUTPUT FLAG:
C             0  - NORMAL EXECUTION.
C             -1.- MISSING DATA FILE.
C             1. - MODIFIED JULIAN DATE LESS THAN THE  FIRST
C                  DATE OF THE DATA FILE.
C             2. - MODIFIED JULIAN  DATE  GREATER  THAN  THE 
C                  LAST DATE OF THE DATA FILE.
C
C OBS:
C
C       THIS ROUTINE USES THE SOLAR  FLUX  AND   GEOMAGNETIC
C       INDEX DATA FILE FLUX_MEAN.DAT
C
C REFERENCES:
C
C   (1) JACCHIA, L. G.  "THERMOSPHERIC TEMPERATURE,  DENSITY
C       AND COMPOSITIONS: NEW MODELS."  CAMBRIDGE,  MA,  SAO
C       1977. (SAO SPECIAL REPORT NO 375)
C
C   (2) JACCHIA, L. G. ATMOSPHERIC MODELS IN THE REGION FROM
C       110 TO 2000 KM. IN COMMITTEE ON SPACE RESEARCH (COS-
C       PAR). "CIRA 1972." BERLIM, AKADEMIC-VERLAG, 1972.
C
C
C AUTHORS:
C
C       VALDEMIR CARRARA       - INPE - S.J.CAMPOS - BR
C
C
C DATE:
C
C       JAN. 1985              V. 1.0
C       SEP  2011              V. 2.0
C
C
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SD(15)
      CHARACTER*30 NAME
      LOGICAL LFLX
      COMMON /FLXDT/ TIDA,TEDA,TADA
C
C------
C
      IF(TIDA.EQ.0.) THEN
         NAME   = 'FLUX_MEAN.DAT'
         INQUIRE(FILE=NAME,EXIST=LFLX)
         IF(.NOT.LFLX) THEN
            OUTR  = -1.
            DO  I = 1, 11
               SD(I)= 0.
            ENDDO
		  RETURN
         ENDIF
	   OPEN(UNIT=20,FILE=NAME, ACCESS='DIRECT', RECL= 49,
     1      FORM='FORMATTED', STATUS='OLD')
         READ(20,REC=1,FMT=2000) NATI,NATF
         DATI   = NATI
         DATF   = NATF
         TIDA   = DATI + 2
         TEDA   = DATF
      ENDIF

      OUTR    = 0.
      IF(RJUD.LT.TIDA) THEN
         OUTR   = 1.
         DO I = 1, 11
            SD(I) = 0.
         ENDDO
         RETURN
      ENDIF

      IF(RJUD.GT.TEDA) THEN
         OUTR   = 2.
         DO I = 1, 11
            SD(I) = 0.
         ENDDO
         RETURN
      ENDIF
      
	INDI    = RJUD - TIDA + 4
      READ(20, REC=INDI, FMT=2010) SD

      DO I = 2, 10
         SD(I) = SD(I)/10.
      ENDDO

      RETURN
 2000 FORMAT(I5,1X,I5)
 2010 FORMAT(F5.0,8F2.0,2F3.0,4F5.1)
      END


      SUBROUTINE SFDJ70(RJUD, DAFR, SF, OUTR)
C
C-----
C
C PURPOSE:
C
C       THE SUBROUTINE SFDJ70 RETRIEVES THE MEAN SOLAR FLUX
C                                               -     -
C       AND GEO-PHYSICAL DATA AT THE DATE RJUD. THE MEAN 
C                        -
C       VALUE IS CALCULATED BASED ON THE ARITMETIC MEAN OVER
C
C       6 SOLAR ROTATIONS ACCORDING TO JACCHIA'S 70 MODEL.
C                                      -         --
C INPUTS:
C
C       RJUD  MODIFIED JULIAN DATE  (IF  OUT  OF  RANGE  THE 
C             ROUTINE WILL RETURN A NON-ZERO VALUE IN OUTR.
C			(REAL*8)
C       DAFR  TIME (UT) OF THE DAY, IN SECONDS (0 TO 86400).
C			(REAL*8)
C
C OUTPUTS:
C       SF(1)    DAILY OBSERVED SOLAR FLUX AT  10.7  CM,  AT
C                THE  TIME  1.71  DAYS  EARLIER,  IN   1E-22
C                W/M/M/HZ.
C       SF(2)    ARITMETIC MEAN OF DAILY OBSERVED SOLAR  FLUX,
C                AS DEFINED BY JACCHIA, IN 1E-22 W/M/M/HZ AT
C                1.71 DAYS EARLIER.
C       SF(3)    3-HOURLY PLANETARY GEOMAGNETIC INDEX KP, AT
C                THE TIME 0.279 DAYS EARLIER.
C
C       OUTR  OUTPUT FLAG:
C             0  - NORMAL EXECUTION.
C             -1.- DATA FILE NOT ON DISK.
C             1. - MODIFIED JULIAN DATE LESS THAN THE  FIRST
C                  DATE OF THE DATA FILE.
C             2. - MODIFIED JULIAN  DATE  GREATER  THAN  THE 
C                  LAST DATE OF THE DATA FILE.
C
C OBS:
C
C       THIS ROUTINE USES THE SOLAR  FLUX  AND   GEOMAGNETIC
C       INDEX DATA FILE FLUX_MEAN.DAT
C
C REFERENCES:
C
C   (1) JACCHIA, L. G. ATMOSPHERIC MODELS IN THE REGION FROM
C       110 TO 2000 KM. IN COMMITTEE ON SPACE RESEARCH (COS-
C       PAR). "CIRA 1972." BERLIM, AKADEMIC-VERLAG, 1972.
C
C AUTHORS:
C
C       VALDEMIR CARRARA       - INPE - S.J.CAMPOS - BR
C
C DATE:
C
C       SEP. 2011              V. 1.0
C
C
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SD(15), SF(3)
      CHARACTER*30 NAME
C
C------
C
      IF(DAFR.LT.61344.) THEN

         CALL SFLUXD(RJUD-2, SD, OUTR)
         IF(OUTR.NE.0.) THEN
            WRITE(6,*) ' ERROR IN ROUTINE SFDJ70: OUTR = ',
     1            INT(OUTR)
            STOP
         ENDIF
         SF(1)   = SD(12)
         SF(2)   = SD(15)
	   TAUO    = DAFR/3600 - 6.696

	   IF (TAUO .LT. 0.0) THEN
            CALL SFLUXD(RJUD-1, SD, OUTR)
	      ND   = INT((TAUO + 24)/3) + 1
	   ELSE
            CALL SFLUXD(RJUD, SD, OUTR)
	      ND   = INT(TAUO/3) + 1
	   ENDIF

	   SF(3)   = SD(ND)

	ELSE

         CALL SFLUXD(RJUD-1, SD, OUTR)
         IF(OUTR.NE.0.) THEN
            WRITE(6,*) ' ERROR IN ROUTINE SFDJ70: OUTR = ',
     1            INT(OUTR)
            STOP
         ENDIF
         SF(1)   = SD(12)
         SF(2)   = SD(15)
	   TAUO    = DAFR/3600 - 6.696

	   IF (TAUO .LT. 0.0) THEN
	      ND   = INT((TAUO + 24)/3) + 1
	   ELSE
            CALL SFLUXD(RJUD, SD, OUTR)
	      ND   = INT(TAUO/3) + 1
	   ENDIF

	   SF(3)   = SD(ND)

	ENDIF

      RETURN
      END

      SUBROUTINE SFDJ77(RJUD, DAFR, SA, GSTI, SF, OUTR)
C
C-----
C
C PURPOSE:
C
C       THE SUBROUTINE SFDJ77 COMPUTES THE MEAN SOLAR FLUX
C                                               -     -
C       AND GEO-PHYSICAL DATA AT THE DATE RJUD. THE MEAN 
C                        -
C       VALUE IS CALCULATED BASED ON THE GAUSSIAN MEAN OVER
C
C       421 DAYS ACCORDING TO JACCHIA'S 77 MODEL.
C                             -         --
C INPUTS:
C
C       RJUD  MODIFIED JULIAN DATE  (IF  OUT  OF  RANGE  THE 
C             ROUTINE WILL RETURN A NON-ZERO VALUE IN OUTR.
C			(REAL*8)
C       DAFR  TIME (UT) OF THE DAY, IN SECONDS (0 TO 86400).
C			(REAL*8)
C       SA(1) RIGHT ASCENSION OF THE  POINT  IN QUESTION, IN
C             RADIANS (0 TO 2.*PI)
C       SA(2) DECLINATION (GEOCENTRIC LATITUDE) OF THE POINT,
C             IN RADIANS (-PI TO PI).
C       GSTI  GREENWICH SIDERAL TIME, IN RADIANS (0 TO 2.*PI),
C             AT THE TIME DAFR OF THE DATE RJUD (REAL*8).
C
C OUTPUTS:
C       SF(1)    DAILY OBSERVED SOLAR FLUX AT  10.7  CM, IN
C                1E-22 W/M/M/HZ.
C       SF(2)    GAUSSIAN MEAN OF DAILY OBSERVED SOLAR  FLUX,
C                AS DEFINED BY JACCHIA, IN 1E-22 W/M/M/HZ.
C       SF(3)    3-HOURLY PLANETARY GEOMAGNETIC INDEX KP, 
C                CALCULATED ACCORDING TO JACCHIA (1).
C
C       OUTR  OUTPUT FLAG:
C             0  - NORMAL EXECUTION.
C             -1.- DATA FILE NOT ON DISK.
C             1. - MODIFIED JULIAN DATE LESS THAN THE  FIRST
C                  DATE OF THE DATA FILE.
C             2. - MODIFIED JULIAN  DATE  GREATER  THAN  THE 
C                  LAST DATE OF THE DATA FILE.
C
C OBS:
C
C       THIS ROUTINE USES THE SOLAR  FLUX  AND   GEOMAGNETIC
C       INDEX DATA FILE FLUX_MEAN.DAT
C
C REFERENCES:
C
C   (1) JACCHIA, L. G.  "THERMOSPHERIC TEMPERATURE,  DENSITY
C       AND COMPOSITIONS: NEW MODELS."  CAMBRIDGE,  MA,  SAO
C       1977. (SAO SPECIAL REPORT NO 375)
C
C AUTHORS:
C
C       VALDEMIR CARRARA       - INPE - S.J.CAMPOS - BR
C
C DATE:
C
C       SEP. 2011              V. 1.0
C
C
      IMPLICIT REAL*8 (A-H,O-Z)
      LOGICAL LFLX
      DIMENSION SD(17), SA(3), SF(3)
      CHARACTER*30 NAME
      COMMON /FLXDT/ TIDA,TEDA,TADA
C
C------
C
      CALL SFLUXD(RJUD, SD, OUTR)

      IF(OUTR.NE.0.) THEN
         WRITE(6,*) ' ERROR IN ROUTINE SFDJ70: OUTR = ',
     1            INT(OUTR)
         STOP
      ENDIF

      SF(1)   = SD(12)
      SF(2)   = SD(14)

      RLAT    = SA(2)
      RLON    = SA(1) - GSTI
      SIFI    = 0.9792*SIN(RLAT) + 
     1          0.2028*COS(RLAT)*COS(RLON - 5.07891)
      COFI    = SQRT(1 - SIFI*SIFI)
      RLAG    = 2.4 + 4.8*COFI*COFI
	TAUO    = DAFR/3600. - RLAG

      IF (TAUO .LT. 0.0) THEN
         CALL SFLUXD(RJUD-1, SD, OUTR)
	   ND   = INT((TAUO + 24)/3) + 1
	ELSE
	   ND   = INT(TAUO/3) + 1
	ENDIF

      SF(3)   = SD(ND)

      RETURN
      END


      FUNCTION GST (DJM, T)
C
C-----
C
C PURPOSE:
C          THE FUNCTION GST FURNISHES THE GREENWICH MEAN 
C          SIDEREAL TIME REFERRED TO THE J2000.0 EQUATOR
C          AND EQUINOX.
C
C INPUT:
C          DJM     MODIFIED JULIAN DATE IN DAYS, REFERRED TO
C                  1950.0.
C          T       UT1 TIME COMPLEMENT IN SECONDS.
C    
C          COMMON/CONST/
C              PI  A DIMENSIONLESS RATIO OF THE CIRCUM-
C                  FERENCE OF A CIRCLE AND ITS DIAMETER
C                  (=3.1415926536...).
C            PIV2  TWO TIMES PI.
C
C          COMMON/EARTH/
C              RT  EARTH'S RADIUS IN METERS.
C            FLAT  FLATTENING PARAMETER.
C              GM  EARTH'S GRAVITATIONAL CONSTANT.
C           TETAP  EARTH'S SIDEREAL ROTATION SPEED IN 
C                  RADIANS/SECOND.
C          
C OUTPUT:
C             GST  GREENWICH SIDEREAL TIME IN RADIANS
C
C SUBCALLS:
C          NONE.
C
C AUTHOR:
C          HELIO KOITI KUGA  APR/89   VERSION 1.0
C
C REF.:
C          SUPPLEMENT TO THE ASTRONOMICAL ALMANAC - PAGE 
C          S15, 1983 (THE ASTRONOMICAL ALMANAC 1984).
C       
      IMPLICIT REAL*8 (A-H,O-Z)

C      COMMON /CONST/ PI,PIV2,PID2,RAD,DEG
C      COMMON /EARTH/ RT,FLAT,GM,TETAP
C
C-----
C
      DATA PI /3.1415926535898/, PIV2 /6.2831853071796/
	DATA TETAP /7.29211585468195D-5/

      TSJ = (DJM-18262.5D0)/36525.D0
      TSG0 = 24110.54841D0 + (8640184.812866D0 + 9.3104D-2*TSJ
     *     - 6.2D-6*TSJ*TSJ) * TSJ
      TSG0 = TSG0*PI/43200.D0
      GST  = MOD(TSG0 + T*TETAP,PIV2)
      IF(GST.LT.0.D0) GST = GST + PIV2
C
      RETURN
      END


      FUNCTION DJM (DIA, AMES, ANO)
C
C-----
C
C PURPOSE:
C          THE FUNCTION DJM FURNISHES THE MODIFIED JULIAN
C          DATE WITH REFERENCE TO THE DAY, MONTH AND YEAR,
C          INPUT REAL PARAMETERS AT ZERO HOURS OF THE DAY.
C
C INPUT:
C          DIA     DAY OF THE MONTH.
C          AMES    MONTH OF THE YEAR.
C          ANO     YEAR.
C
C OUTPUT:
C          DJM     MODIFIED JULIAN DATE IN DAYS, REFERRED TO
C                  1950.0. 
C
C SUBCALLS:
C          NONE.
C 
C REMARKS:
C          THIS FUNCTION IS OPTIMIZED FOR THE PERIOD BETWEEN
C          THE YEARS 1900-2100.
C
C
      IMPLICIT REAL*8 (A-H,O-Z)
C
C-----
C
      DJM = 367.D0*ANO + DIA - 712269.D0
     *    + DINT(275.D0*AMES/9.D0)
     1    - DINT(7.D0*(ANO+DINT((AMES+9.D0)/12.D0))/4.D0)
C
      RETURN
      END


      SUBROUTINE DJMINV (DIJM, IDAY, IMONTH, IYEAR)
C
C-----
C
C PURPOSE:
C          THE SUBROUTINE DJMINV COMPUTES THE DAY, MONTH
C          AND YEAR (INTEGER NUMBERS) CORRESPONDING TO THE
C          MODIFIED JULIAN DATE DIJM.
C
C INPUT:
C          DIJM    MODIFIED JULIAN DATE IN DAYS, REFERRED TO
C                  1950.0.
C
C OUTPUT:
C          IDAY    DAY OF THE MONTH.
C          IMONTH  MONTH OF THE YEAR.
C          IYEAR   YEAR.      
C
C SUBCALLS:
C          NONE.
C
C 

      IMPLICIT REAL*8 (A-H,O-Z)

      INTEGER*4  D1(13),A400,A100,A4,A1,D

      DATA D1/0,31,61,92,122,153,184,214,245,275,306,337,366/
C
C-----
C
C      D    = INT4(DIJM + 127775.D0)
      D    = INT(DIJM + 127775.D0)
      A400 = D/146097
      D    = D - A400*146097
      A100 = D/36524
      D    = D - A100*36524
	A4   = 0
	A1   = 0
      IF(A100.GT.3) GO TO 2
      A4   = D/1461
      D    = D - A4*1461
      A1   = D/365
      IF(A1.GT.3) GO TO 2
      D    = D - A1*365
      I    = D/32 + 2
      D    = D + 1
   1  IF(D1(I).GE.D) GO TO 3
      I    = I + 1
      GO TO 1
C
   2  IDAY   = 29
      IMONTH = 2
      IYEAR  = 1600 + A400*400 + A100*100 + A4*4 + A1
      RETURN
C
   3  IMONTH  = I + 1
      IDAY    = D - D1(I-1)
      IYEAR   = 1600 + A400*400 + A100*100 + A4*4 + A1
      IF(IMONTH.LT.13) RETURN
      IMONTH  = IMONTH - 12
      IYEAR   = IYEAR + 1
      RETURN
C
      END

